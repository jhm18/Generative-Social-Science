. Name:     region_base.dp
. Purpose:  Draw a filled region on a plot
.
. Step 0:   Define the output devices
.
let string fplot = region_base.ps
set ipl1na ^fplot
device 2 postscript
device 2 color on
.
. Step 1:   Read the data
.
SKIP 3
READ kld_11_density.DAT p_x p_y
SKIP 0
SET WRITE DECIMALS 3
.
LET YTEMP = P_Y
LET XTEMP = P_X
RETAIN YTEMP XTEMP  SUBSET XTEMP = 0 TO 0.047
.
.           Find the closest matches to the 5-th and 95-th points
.
LET YSUM   = SUM YTEMP
LET YCUM   = CUMULATIVE SUM YTEMP
LET YZ     = YCUM/YSUM
LET CUTOFF = DATA  0.05 0.95
LET XINDX  = MATCH YZ CUTOFF
LET IVAL   = XINDX(1)
LET XLOW   = XTEMP(IVAL)
LET IVAL   = XINDX(2)
LET XUPP   = XTEMP(IVAL)
.
. Step 2:   Set some plot control
.
MAJOR TIC MARK NUMBER 5
X1LIMIT 0  0.047
.
REGION FILL OFF ON ON
REGION BASE 0 ALL
.
. Step 3:   Generate the plot
.
PLOT YTEMP XTEMP AND
PLOT YTEMP XTEMP  SUBSET XTEMP <= XLOW AND
PLOT YTEMP XTEMP  SUBSET XTEMP >= XUPP
.
. PLOT p_y p_x  subset p_x = 0 to 0.047
.
. Step 4:   Close
.
device 2 close
system ps2pdf ^fplot
.
