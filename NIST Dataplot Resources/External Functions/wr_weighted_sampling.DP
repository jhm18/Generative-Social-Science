ECHO OFF
FEEDBACK OFF

.   Generating Weighted Samples with Replacement
.   Alan Heckert & Jonathan H. Morgan
.   23 June 2021

.   Function takes four arguments: n_samp,  maxloop, weights, x
.   n_samp refers to the number of random samples to be generated by the routine.
.   maxloop refers to the number of samples from which random samples will be drawn, the k in n choose k.
.   weights refers to the weights being used for the sampling.
.   x refers to the variable being sampled.

.   The function returns a weighted sample of length n_samp.
.   
.   Note:   The routine peforms weighted samples by comparing the weights to samples generated from a uniform random distribution 
.           scaled to encapsulate the range of the weights. 
.           The routine's scale parameters reduce the number of maxloop draws necessary to generate the sample.
.           Random draws are included in the random sample if the sample's weight is greater than the random
.           draw from the comparison distribution.

COMMENT Initialize Settings
    SET RANDOM NUMBER GENERATOR FIBONACCI CONGRUENTIAL
    SEED 33497

COMMENT Perform Sampling
    .   Specifying Location Parameters
        LET LOC = MINIMUM weights
        LET SCALE = MAXIMUM weights
        LET i_loc = 1
        LET i_scale = SIZE x

    .   Generate Two Sets of Uniform Random Numbers
        LET i_value = UNIFORM RANDOM NUMBERS FOR I = 1 1 MAXLOOP
        LET i_value = LOC + SCALE*i_value

        LET ind = UNIFORM RANDOM NUMBERS FOR I = 1 1 MAXLOOP
        LET ind = i_loc + i_scale*ind
        LET index = CEIL(ind) - 1

    .   Now Generate the Weighted Samples
        LET index2 = SEQUENCE 1 1 MAXLOOP
        LET val_i  = GATHER i_value index2
        LET val    = GATHER weights index

        LET TAG = 0 FOR I = 1 1 MAXLOOP
        LET diff = val - val_i
        LET TAG = 1 SUBSET diff > 0
        RETAIN index SUBSET TAG = 1
        RETAIN index FOR I = 1 1 N_SAMP
        LET samples = GATHER x index
        DELETE n_samp maxloop loc scale i_loc i_scale i_value index ind index2 val val_i tag diff

COMMENT Example CALL
    . LET n_samp = 10000
    . LET maxloop = 200000
    . NAME p_grid x
    . NAME post weights
    . CALL /Users/jonathan.h.morgan/Dataplot_Resources/Dataplot_Utilities/wr_weighted_sampling.DP 

FEEDBACK ON
ECHO ON
